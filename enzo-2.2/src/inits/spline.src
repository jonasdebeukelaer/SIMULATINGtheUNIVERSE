      subroutine spline(x,y,n,yp1,ypn,y2)

      implicit none

!     Parameters

      integer, parameter :: nmax = 1000

!     Arguments

      integer :: n
      real :: x(n), y(n), y2(n)
      real :: yp1, ypn

!     Locals

      real :: u(nmax)
      real :: qn, un, p, sig
      integer :: i, k

      if (yp1 .gt. 0.99e+30) then
        y2(1) = 0.0
        u(1) = 0.0
      else
        y2(1) = -0.5
        u(1) = (3.0/(x(2)-x(1)))*((y(2)-y(1))/(x(2)-x(1))-yp1)
      endif

      do i = 2, n-1
        sig = (x(i)-x(i-1))/(x(i+1)-x(i-1))
        p = sig*y2(i-1)+2.0
        y2(i) = (sig-1.0)/p
        u(i) = (6.0*((y(i+1)-y(i))/(x(i+1)-x(i))-(y(i)-y(i-1))
     &         /(x(i)-x(i-1)))/(x(i+1)-x(i-1))-sig*u(i-1))/p
      end do

      if (ypn .gt. 0.99e+30) then
        qn = 0.0
        un = 0.0
      else
        qn = 0.5
        un = (3.0/(x(n)-x(n-1)))*(ypn-(y(n)-y(n-1))/(x(n)-x(n-1)))
      endif

      y2(n) = (un-qn*u(n-1))/(qn*y2(n-1)+1.0)

      do k = n-1, 1, -1
        y2(k) = y2(k)*y2(k+1)+u(k)
      end do

      return
      end



      subroutine splint(xa,ya,y2a,n,x,y)

      implicit none

!     Arguments

      integer :: n
      real :: xa(n), ya(n), y2a(n)
      real :: x, y

!     Locals

      integer :: klo, khi, k
      real :: h, a, b

      klo = 1
      khi = n

    1 continue
      if (khi-klo > 1) then
        k = (khi+klo)/2
        if(xa(k) > x)then
          khi = k
        else
          klo = k
        endif
      goto 1
      endif

      h = xa(khi)-xa(klo)

      if (h == 0.0) stop 'bad xa input.'

      a = (xa(khi)-x)/h
      b = (x-xa(klo))/h
      y = a*ya(klo)+b*ya(khi)+
     &      ((a**3-a)*y2a(klo)+(b**3-b)*y2a(khi))*(h**2)/6.0

      return
      end
