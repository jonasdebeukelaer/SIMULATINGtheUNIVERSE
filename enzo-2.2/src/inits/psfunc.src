!=======================================================================
!//////////////////////////  FUNCTION PS_FUNC  \\\\\\\\\\\\\\\\\\\\\\\\\

      real function psfunc(k, ispectrum, omega0, hub, omega_nu,
     &                  omega_lam, psindex, omegab0, z, gamma,
     &                  psnorm, growth_factor, kcutoff)

!  written by: Greg Bryan
!  date:       May, 1996
!  modified:   Robert Harkness
!  date:       November, 2003
!
!  PURPOSE: Generalized power spectrum function
!
!  INPUT: 
!    k        - wavenumber (in Mpc^-1)
!    (also from cosmo_parm, including:
!    ipower_spectrum - indicates which function to use)
!
!  OUTPUT:
!    ps_func  - spectral density (Mpc^3)

      implicit none

!     Arguments

      real :: k
      integer :: ispectrum
      real :: omega0, hub, omega_nu, omega_lam, psindex,
     &        omegab0, z, gamma
      real :: psnorm, growth_factor, kcutoff

!     1) CDM declarations

      real, parameter :: T0 = 2.726
      real :: q, result

!     2) CHDM declarations

      real, parameter :: d1 = 0.004321,
     &                   d2 = 2.217e-6,
     &                   d3 = 11.63,
     &                   d4 = 3.317
      real :: pscold, ptotal, gamma_nu, x, x0

!     4) Read a spectrum declarations

      integer, parameter :: max_points=400
      integer :: i, npoints
      logical :: found, first_call
      real :: kvalue(max_points), power(max_points), dummy
      character (len = 80) :: filename, line

      data first_call /.true./
      save npoints, kvalue, power, first_call


!     Below the spectral cutoff (if present), set power to zero

      if (k .lt. kcutoff) then
         psfunc = 0.0
         return
      endif

      if (ispectrum .eq. 1) then

!  1) CDM Power-Spectrum Bardeen et al 1986 augmented by:
!       Peacock and Dodds 1994 (MNRAS 267, 1020), 
!       Sugiyama 1995 (ApJS 100, 281)

!     Compute shape parameter

         gamma = omega0*hub*(T0/2.7)**(-2)*
     &     exp(-omegab0*(1 + sqrt(2*hub)/omega0))

!    &     exp(-2.0*omegab0)  ! old

         q = k/(gamma*hub)

!     Compute z=0 spectrum

         result = psnorm * k**psindex * 
     &         (log(1.0+2.34*q)/(2.34*q))**2 *
     &         (1.0 + 3.89*q + (16.1*q)**2 + (5.46*q)**3 + 
     &         (6.71*q)**4)**(-0.5)

!     Use growth factor to linearly scale to requested z

         psfunc = result * growth_factor**2
         return

      elseif (ispectrum .eq. 2) then

!  2) CHDM Power-Spectrum From Ma (1996) -- 1 massive neutrino

!     First, compute cold spectrum as above
     
         gamma = omega0*hub*(T0/2.7)**(-2)*
     &     exp(-omegab0*(1 + sqrt(2*hub)/omega0))

         q = k/(gamma*hub)
         result = psnorm * k**psindex * 
     &         (log(1.0+2.34*q)/(2.34*q))**2 *
     &         (1.0 + 3.89*q + (16.1*q)**2 + (5.46*q)**3 + 
     &         (6.71*q)**4)**(-0.5)
         pscold = result * growth_factor**2

!     Now modify with Ma eq. 11 (does this assume EdS?)

         gamma_nu = sqrt(1.0/(1.0+z)) * omega_nu * hub**2
         x  = k/gamma_nu
         x0 = k/ (omega_nu * hub**2)
         ptotal = pscold * ( (1.0 + d1*x**(d4/2.0) + d2*x**d4) /
     &                       (1.0 + d3*x0**d4)       )**(omega_nu**1.05)
         psfunc = ptotal
         return

      elseif (ispectrum .eq. 3) then

!  3) Power-law spectrum (scale-free)

         result = psnorm * k**psindex
         psfunc = result * growth_factor**2
         return

      elseif (ispectrum .eq. 4) then

!  4) Read a power spectrum from a file
!     Spectrum must power ordered by increasing k values
!     (format: dummy, k, P(k) on each line, comment symbol is #)

!     This version ignores the normalization and z, etc.

!        If this is the first call call, prompt for name and read spectrum

         if (first_call) then

            write(*,*) 'Enter ps name:'
            read(*,'(a80)') filename

            open(1, file=filename, status='old')

!           Loop until done (ignore lines starting with #)

            npoints = 0

            do while (.true.)
               read(1, '(a80)', end=100) line
               if (line(1:1) .ne. '#') then
                  npoints = npoints + 1
                  read(line, *) dummy, kvalue(npoints), power(npoints)
!                 write(0,*) npoints, kvalue(npoints), power(npoints)
               endif
            enddo

 100        continue

!           Close file and set first_call to false

            close(1)

            first_call = .false.

         endif

!        Search for given k value in spectrum

         found = .false.
         i = 0

         do while (.not. found .and. i .lt. npoints)
            i = i + 1
            if (k .lt. kvalue(i)) found = .true.
         enddo

!        Linearly interpolate or set power to zero if outside spectrum

         if (i .eq. 1 .or. .not. found) then
            result = 0.0
         else
            result = power(i-1) + (power(i)    - power(i-1)) *
     &                           (k           - kvalue(i-1))/
     &                           (kvalue(i)   - kvalue(i-1))
         endif

         psfunc = result
         return

      elseif (ispectrum .eq. 5) then

!  5) CHDM Power-Spectrum From Ma (1996) -- 2 neutrinos, equal masses
!     (I just doubled the free-streaming length)

!     First, compute cold spectrum as above
     
         gamma = omega0*hub*(T0/2.7)**(-2)*
     &     exp(-omegab0*(1 + sqrt(2*hub)/omega0))

         q = k/(gamma*hub)
         result = psnorm * k**psindex * 
     &         (log(1.0+2.34*q)/(2.34*q))**2 *
     &         (1.0 + 3.89*q + (16.1*q)**2 + (5.46*q)**3 + 
     &         (6.71*q)**4)**(-0.5)
         pscold = result * growth_factor**2

!     Now modify with Ma eq. 11 (does this assume EdS?)
!       (just half gamma_nu to reflect the doubled velocity)

         gamma_nu = sqrt(1.0/(1.0+z)) * omega_nu * hub**2 / 2.0
         x  = k/gamma_nu
         x0 = k/(omega_nu * hub**2 / 2.0)
         ptotal = pscold * ( (1.0 + d1*x**(d4/2.0) + d2*x**d4) /
     &                       (1.0 + d3*x0**d4)       )**(omega_nu**1.05)

         psfunc = ptotal
         return

      elseif (ispectrum .eq. 6) then

!  6) CDM-like power spectrum with a fixed shape parameter.
!
!  BWO: This is included for historical interest.  The spectrum is
!  the same as the bardeen power spectrum (type #1) but with a free
!  parameter, gamma, that allows the spectral shape to be modified 
!  independently of the cosmological parameters.  This is now
!  HIGHLY DEPRECATED (i.e., wrong)

!     Compute shape parameter

!        gamma = 0.25   (now set in common block)

         q = k/(gamma*hub)

!     Compute z=0 spectrum

         result = psnorm * k**psindex * 
     &         (log(1.0+2.34*q)/(2.34*q))**2 *
     &         (1.0 + 3.89*q + (16.1*q)**2 + (5.46*q)**3 + 
     &          (6.71*q)**4)**(-0.5)

!     use growth factor to linearly scale to requested z

         psfunc = result * growth_factor**2
         return

      else

         write(0,*) 'PSFUNC: unrecognized ispectrum:', ispectrum
         stop

      endif

      end
